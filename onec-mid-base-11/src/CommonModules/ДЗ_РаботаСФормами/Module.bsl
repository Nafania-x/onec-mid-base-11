#Область СлужебныйПрограммныйИнтерфейс

	// ААА+
	// Арсенина А.А.
	// Добавление поля "Контактное лицо", команды "Пересчитать Таблицу" и Элемента "Согласованная скидка" 
	// для введения пользователем значения скидки и возможности пересчета таблицы с ее учетом. 
	// Определение расположения новых элементов и команды на форме.
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, из которой выполняется команда.
//
Процедура ДополнитьФорму (Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		
		НоваяГруппа = Форма.Элементы.Вставить ("ГруппаСкидкаПересчитатьТаблицу", Тип("ГруппаФормы"),,Форма.Элементы.Страницы);
		НоваяГруппа.Заголовок = "Группа скидка пересчитать таблицу";
		НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
		НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
		
		НоваяКоманда = Форма.Команды.Добавить("ПересчитатьТаблицу");
		НоваяКоманда.Заголовок = "Пересчитать таблицу";
		НоваяКоманда.Действие = "ДЗ_ПересчитатьТаблицу";
	
		Кнопка = Форма.Элементы.Добавить("КнопкаКомандыПересчитатьТаблицу", Тип("КнопкаФормы"),Форма.Элементы.ГруппаСкидкаПересчитатьТаблицу);
		Кнопка.ИмяКоманды = "ПересчитатьТаблицу";
		Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		
		ПолеВвода = Форма.Элементы.Вставить ("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидкаПересчитатьТаблицу,Форма.Элементы.КнопкаКомандыПересчитатьТаблицу);
		ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
		ПолеВвода.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
		ПолеВвода.УстановитьДействие ("ПриИзменении","ДЗ_СогласованнаяСкидкаПриИзменении");

	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента (Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента (Форма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	// ААА+
	// Арсенина А.А.
	//Добавление поля "Контактное лицо" на формы документов 
	//с определением места размещения в группе "ГруппаШапкаПраво" в конце 
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
		
	ПолеВвода = Форма.Элементы.Добавить ("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";

КонецПроцедуры

	// ААА+
	// Арсенина А.А.
	//Добавление поля "Контактное лицо" на формы документов 
	//с определением места размещения над полем "Сумма документа"
Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента (Форма)

	ПолеВвода = Форма.Элементы.Вставить ("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	
КонецПроцедуры

#КонецОбласти
